apply plugin: 'com.android.library'

buildscript {
    repositories {
        google()
        maven { url "https://jitpack.io" }
        jcenter()
    }

    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintrayVersion"
        classpath "com.github.dcendents:android-maven-gradle-plugin:$mavenGradleVersion"
    }
}

apply plugin: 'com.jfrog.bintray'

android {

    compileSdkVersion Integer.parseInt(project.ANDROID_COMPILE_SDK_VERSION)

    defaultConfig {
        minSdkVersion Integer.parseInt(project.MIN_SDK)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}


dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':chat-sdk-firebase-adapter')
    implementation project(':chat-sdk-core')
    implementation project(':chat-sdk-ui')

    implementation "com.google.firebase:firebase-auth:$firebaseAuthVersion"
    implementation "com.google.android.gms:play-services-auth:$playAuthVersion"
    implementation "com.facebook.android:facebook-login:$facebookLoginVersion"
    implementation "com.twitter.sdk.android:twitter:$twitterVersion"

    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"

}

// Bintray upload properties
Properties properties = new Properties()
File localProperties = project.rootProject.file('local.properties')
if (localProperties.exists()) {
    properties.load(localProperties.newDataInputStream())
    if (properties.getProperty('bintray.enabled') != null) {

        group = CHAT_SDK_GROUP
        version = CHAT_SDK_VERSION

        ext {
            bintrayRepo = BINTRAY_REPO
            bintrayName = 'chat-sdk-firebase-social-login'
            bintrayUserOrg = BINTRAY_USER_ORG

            publishedGroupId = CHAT_SDK_GROUP
            libraryName = 'ChatSDKFirebaseSocialLogin'
            artifact = 'chat-sdk-firebase-social-login'

            libraryDescription = 'Chat SDK Firebase Social Login'

            siteUrl = SITE_URL
            gitUrl = GIT_URL

            libraryVersion = CHAT_SDK_VERSION

            developerId = DEVELOPER_ID
            developerName = DEVELOPER_NAME
            developerEmail = DEVELOPER_EMAIL

            licenseName = LICENSE_NAME
            licenseUrl = LICENSE_URL
            allLicenses = ['GPL-3.0']
        }

        apply from: 'https://raw.githubusercontent.com/chat-sdk/JCenter/master/installv1.gradle'
        apply from: 'https://raw.githubusercontent.com/chat-sdk/JCenter/master/bintrayv1.gradle'
    }
}



