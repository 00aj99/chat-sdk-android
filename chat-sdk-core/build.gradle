apply plugin: 'com.android.library'

buildscript {
    repositories {
        google()
        maven { url "https://jitpack.io" }
        jcenter()
    }

    dependencies {
        classpath "org.greenrobot:greendao-gradle-plugin:$greenDaoVersion"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintrayVersion"
        classpath "com.github.dcendents:android-maven-gradle-plugin:$mavenGradleVersion"
    }
}


apply plugin: 'org.greenrobot.greendao'

android {

    compileSdkVersion Integer.parseInt(project.ANDROID_COMPILE_SDK_VERSION)

    defaultConfig {
        minSdkVersion Integer.parseInt(project.MIN_SDK)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

greendao {
    schemaVersion 4
    targetGenDir "src/main/java"
    daoPackage "co.chatsdk.core.dao"
}

dependencies {

//    api fileTree(include: ['*.jar'], dir: 'libs')

    implementation "org.greenrobot:greendao:$greenDaoVersion"
    api "joda-time:joda-time:$jodaTimeVersion"
    api "com.jakewharton.timber:timber:$timberVersion"
    api "com.google.code.gson:gson:$gsonVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "org.apache.commons:commons-lang3:$apacheCommonsVersion"
    implementation "com.google.android.gms:play-services-maps:$playMapsVersion"
    implementation "com.google.android.gms:play-services-basement:16.1.0"


    implementation "com.facebook.fresco:fresco:$frescoVersion"
    implementation "id.zelory:compressor:$compressorVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$androidxLifecycleExtensionsVersion"

}

// Bintray upload properties
Properties properties = new Properties()
File localProperties = project.rootProject.file('local.properties')
if (localProperties.exists()) {
    properties.load(localProperties.newDataInputStream())
    if (properties.getProperty('bintray.enabled') != null) {

        group = CHAT_SDK_GROUP
        version = CHAT_SDK_VERSION

        ext {
            bintrayRepo = BINTRAY_REPO
            bintrayName = 'chat-sdk-core'
            bintrayUserOrg = BINTRAY_USER_ORG

            publishedGroupId = CHAT_SDK_GROUP
            libraryName = 'ChatSDKCore'
            artifact = 'chat-sdk-core'

            libraryDescription = 'Chat SDK Core'

            siteUrl = SITE_URL
            gitUrl = GIT_URL

            libraryVersion = CHAT_SDK_VERSION

            developerId = DEVELOPER_ID
            developerName = DEVELOPER_NAME
            developerEmail = DEVELOPER_EMAIL

            licenseName = LICENSE_NAME
            licenseUrl = LICENSE_URL
            allLicenses = ['GPL-3.0']
        }

        apply from: 'https://raw.githubusercontent.com/chat-sdk/JCenter/master/installv1.gradle'
        apply from: 'https://raw.githubusercontent.com/chat-sdk/JCenter/master/bintrayv1.gradle'
    }
}


